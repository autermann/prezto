# vim:ft=zsh ts=2 sw=2 sts=2
#
# agnoster's Theme - https://gist.github.com/3712874
# A Powerline-inspired theme for ZSH
#
# # README
#
# In order for this theme to render correctly, you will need a
# [Powerline-patched font](https://gist.github.com/1595572).
#
# In addition, I recommend the
# [Solarized theme](https://github.com/altercation/solarized/) and, if you're
# using it on Mac OS X, [iTerm 2](http://www.iterm2.com/) over Terminal.app -
# it has significantly better color fidelity.
#
# # Goals
#
# The aim of this theme is to only show you *relevant* information. Like most
# prompts, it will only show git information when in a git working directory.
# However, it goes a step further: everything from the current user and
# hostname to whether the last call exited with an error to whether background
# jobs are running in this shell will all be displayed automatically when
# appropriate.

CURRENT_BG='NONE'
SEGMENT_SEPARATOR='\ue0b0'

function prompt_agnoster_segment {
  local bg fg
  [[ -n $1 ]] && bg="%K{$1}" || bg="%k"
  [[ -n $2 ]] && fg="%F{$2}" || fg="%f"
  if [[ $CURRENT_BG != 'NONE' && $1 != $CURRENT_BG ]]; then
    echo -n " %{$bg%F{$CURRENT_BG}%}$SEGMENT_SEPARATOR%{$fg%} "
  else
    echo -n "%{$bg%}%{$fg%} "
  fi
  CURRENT_BG=$1
  [[ -n $3 ]] && echo -n $3
}

function prompt_agnoster_end {
  if [[ -n $CURRENT_BG ]]; then
    echo -n " %{%k%F{$CURRENT_BG}%}$SEGMENT_SEPARATOR"
  else
    echo -n "%{%k%}"
  fi
  echo -n "%{%f%}"
  CURRENT_BG=''
}

function prompt_agnoster_context {
  local user=$(whoami)
  zstyle -s ':prezto:module:prompt:agnoster' default-user "DEFAULT_USER"
  if [[ "$user" != "$DEFAULT_USER" || -n "$SSH_CLIENT" ]]; then
    prompt_agnoster_segment black default "%(!.%{%F{yellow}%}.)$user@%m"
  fi
  unset DEFAULT_USER
}

function prompt_agnoster_svn {
	local rev st color
	rev=$(svn info 2>/dev/null | awk '/Revision/{print $2}')
	if [[ -n $rev ]]; then
			st="$(svn status 2>/dev/null)"
			if [[ "$st" =~ '.*^ {0,6}C' ]]; then
        color=red
			elif [[ "$st" =~ '.*^[ADMR!?]' ]]; then
        color=yellow
			else
        color=green
			fi
      prompt_agnoster_segment $color black "r$rev"
	fi
}

function prompt_agnoster_pwd {
	local pwd="${PWD/#$HOME/~}"
	if [[ "$pwd" == (#m)[/~] ]]; then
    pwd="$MATCH"
		unset MATCH
	else
    pwd="${${${(@j:/:M)${(@s:/:)pwd}##.#?}:h}%/}/${pwd:t}"
	fi
  prompt_agnoster_segment blue black "$pwd"
}

function prompt_agnoster_status {
  local symbols
  symbols=()
  [[ $RETVAL -ne 0 ]] && symbols+="%{%F{red}%}✘"
  [[ $UID -eq 0 ]] && symbols+="%{%F{yellow}%}⚡"
  [[ $(jobs -l | wc -l) -gt 0 ]] && symbols+="%{%F{cyan}%}⚙"
  [[ -n "$symbols" ]] && prompt_agnoster_segment black default "$symbols"
}

function prompt_agnoster_git {
  local bg st br
  if git-info; then
    [[ -n "$git_info[status]" ]] && bg="yellow" || bg="green"
    st=${git_info[status]:+$git_info[status] }
    br=$(coalesce $git_info[branch] $git_info[commit])
    prompt_agnoster_segment $bg black "\ue0a0 $br$st"
    [[ -n "$git_info[action]" ]] && prompt_agnoster_segment black red "$git_info[action]"
  fi
}

function prompt_agnoster_build {
  RETVAL=$?
  prompt_agnoster_status
  prompt_agnoster_context
  prompt_agnoster_pwd
  prompt_agnoster_svn
  prompt_agnoster_git
  prompt_agnoster_end
}

function prompt_agnoster_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  zstyle ':prezto:module:editor:info:completing' format '%B%F{red}...%f%b'
  zstyle ':prezto:module:editor:info:keymap:primary:overwrite' format ' %F{red}♺%f'

  zstyle ':prezto:module:git:info' verbose 'yes'
  zstyle ':prezto:module:git:info:action' format '%s'
  zstyle ':prezto:module:git:info:added' format ' ✚'
  zstyle ':prezto:module:git:info:ahead' format ' ⬆'
  zstyle ':prezto:module:git:info:behind' format ' ⬇'
  zstyle ':prezto:module:git:info:branch' format '%b'
  zstyle ':prezto:module:git:info:commit' format '%.7c'
  zstyle ':prezto:module:git:info:deleted' format ' ✖'
  zstyle ':prezto:module:git:info:modified' format ' ✱'
  zstyle ':prezto:module:git:info:position' format ':%p'
  zstyle ':prezto:module:git:info:renamed' format ' ➜'
  zstyle ':prezto:module:git:info:stashed' format ' ✭'
  zstyle ':prezto:module:git:info:unmerged' format ' ═'
  zstyle ':prezto:module:git:info:untracked' format ' ◼'
  zstyle ':prezto:module:git:info:keys' format \
    'branch' '%b' \
    'commit' '%c' \
    'action' '%s' \
    'status' '%A%B%S%a%d%m%r%U%u'

  PROMPT='%{%f%b%k%}$(prompt_agnoster_build) '
  RPROMPT='%{%K{black}%F{yellow}%}%}%T%{%f%k%}'
  SPROMPT='zsh: correct %F{red}%R%f to %F{green}%r%f [nyae]? '
}

prompt_agnoster_setup "$@"
